  \newcommand{\imports}{%
\begin{code}
module UntypedPhiCalculus where

open import Axiom.Extensionality.Propositional

open import Relation.Binary.PropositionalEquality using (_≡_)
open import Data.Nat using (ℕ; zero; suc)
open import Data.Empty using (⊥; ⊥-elim)
open import Data.Sum.Base using (_⊎_; inj₁; inj₂; [_,_]′)

open import Agda.Builtin.Sigma using (Σ; _,_; fst; snd)
open import Agda.Builtin.Unit using (⊤; tt)

open import Relation.Nullary
open import Relation.Nullary.Decidable
open import Relation.Binary.Core

open import Function.Base using (id)

infixl 6 _,,_
infix 4 _∋_
infix 4 _∣_⊢_
infix 7 _[_↦_]
infix 2 _⟿_
infixl 4 _∙_
infix 2 _⇛_

\end{code}}

\newcommand{\attrDefs}{%
\begin{code}

data Type : Set where
  ★ : Type

data Phi : Set where
  φ : Phi

data ∅ : Set where
  void : ∅

data None : Set where
  empty : None

data Parent : Set where
  none   : Parent
  _,,_ : Parent → Type → Parent

\end{code}}

\newcommand{\parentDef}{%
\begin{code}

data _∋_ : Parent → Type → Set where

  Z : ∀ {Γ A}
     ---------
   → (Γ ,, A) ∋ A

  S_ : ∀ {Γ A B}
    → Γ ∋ A
      --------
    → (Γ ,, B) ∋ A

\end{code}}

\newcommand{\termDef}{%
\begin{code}

data _∣_⊢_ : Set → Parent → Type → Set where

  `_ : ∀ {L Γ A} 
    → Γ ∋ A
      --------
    → L ∣ Γ ⊢ A
  
  _∙_ : ∀ {L Γ}
    → L ∣ Γ ⊢ ★
    → (L ⊎ Phi)
      -----------
    → L ∣ Γ ⊢ ★
  
  makeObject_ : ∀ {L Γ}
    → ((L ⊎ Phi) → ((L ∣ (Γ ,, ★) ⊢ ★) ⊎ None ⊎ ∅))
      --------------------------------------
    → L ∣ Γ ⊢ ★
     
  _[_↦_] : ∀ {L Γ}
    → L ∣ Γ ⊢ ★
    → L
    → L ∣ Γ ⊢ ★
      ------------
    → L ∣ Γ ⊢ ★

attached_ : ∀ {L Γ} → (t : L ∣ Γ ⊢ ★) → ((L ∣ Γ ⊢ ★) ⊎ None ⊎ ∅)
attached_ = inj₁

missing_ : ∀ {L Γ} → (v : None) → ((L ∣ Γ ⊢ ★) ⊎ None ⊎ ∅)
missing v = inj₂ (inj₁ v)

void_ : ∀ {L Γ} → (v : ∅) → ((L ∣ Γ ⊢ ★) ⊎ None ⊎ ∅)
void v = inj₂ (inj₂ v)



\end{code}}

\newcommand{\countDef}{%
\begin{code}

count : ∀ {Γ} → ℕ → Γ ∋ ★
count {Γ ,, ★} zero     =  Z
count {Γ ,, ★} (suc n)  =  S (count n)
count {∅}     _        =  ⊥-elim impossible
  where postulate impossible : ⊥

ρ_ : ∀ {L Γ} → ℕ → L ∣ Γ ⊢ ★
ρ n  =  ` count n

\end{code}}

\newcommand{\emptyObj}{%
\begin{code}

emptyObjImpl : ∀ {L Γ} 
  → ((L ⊎ Phi) → ((L ∣ (Γ ,, ★) ⊢ ★) ⊎ None ⊎ ∅))
emptyObjImpl _ = inj₂ (inj₁ empty)

emptyObj : ∀ {L Γ} → L ∣ Γ ⊢ ★
emptyObj = makeObject emptyObjImpl

\end{code}}

\newcommand{\exLabel}{%
\begin{code}

data ExampleLabel : Set where
  x : ExampleLabel
  y : ExampleLabel
  z : ExampleLabel
  w : ExampleLabel

exampleLabelComparator : (a : ExampleLabel ⊎ Phi) 
  → (b : ExampleLabel ⊎ Phi) → Dec (a ≡ b)
exampleLabelComparator (inj₁ x) (inj₁ x) = yes _≡_.refl
exampleLabelComparator (inj₁ x) (inj₁ y) = no (λ ())
exampleLabelComparator (inj₁ x) (inj₁ z) = no (λ ())
exampleLabelComparator (inj₁ x) (inj₁ w) = no (λ ())
exampleLabelComparator (inj₁ x) (inj₂ φ) = no (λ ())
exampleLabelComparator (inj₁ y) (inj₁ x) = no (λ ())
exampleLabelComparator (inj₁ y) (inj₁ y) = yes _≡_.refl
exampleLabelComparator (inj₁ y) (inj₁ z) = no (λ ())
exampleLabelComparator (inj₁ y) (inj₁ w) = no (λ ())
exampleLabelComparator (inj₁ y) (inj₂ φ) = no (λ ())
exampleLabelComparator (inj₁ z) (inj₁ x) = no (λ ())
exampleLabelComparator (inj₁ z) (inj₁ y) = no (λ ())
exampleLabelComparator (inj₁ z) (inj₁ z) = yes _≡_.refl
exampleLabelComparator (inj₁ z) (inj₁ w) = no (λ ())
exampleLabelComparator (inj₁ z) (inj₂ φ) = no (λ ())
exampleLabelComparator (inj₁ w) (inj₁ x) = no (λ ())
exampleLabelComparator (inj₁ w) (inj₁ y) = no (λ ())
exampleLabelComparator (inj₁ w) (inj₁ z) = no (λ ())
exampleLabelComparator (inj₁ w) (inj₁ w) = yes _≡_.refl
exampleLabelComparator (inj₁ w) (inj₂ φ) = no (λ ())
exampleLabelComparator (inj₂ φ) (inj₁ x) = no (λ ())
exampleLabelComparator (inj₂ φ) (inj₁ y) = no (λ ())
exampleLabelComparator (inj₂ φ) (inj₁ z) = no (λ ())
exampleLabelComparator (inj₂ φ) (inj₁ w) = no (λ ())
exampleLabelComparator (inj₂ φ) (inj₂ φ) = yes _≡_.refl

\end{code}}

\newcommand{\postulate}{%
\begin{code}

postulate
  φ-extensionality : ∀ {L Γ} 
    (f g : (L ⊎ Phi) → ((L ∣ Γ ⊢ ★) ⊎ None ⊎ ∅)) →
    (∀ x → f x ≡ g x) → f ≡ g

\end{code}}

\newcommand{\exampleObj}{%
\begin{code}

exampleObjectImpl : ∀ {Γ} → ((ExampleLabel ⊎ Phi) 
  → ((ExampleLabel ∣ (Γ ,, ★) ⊢ ★) ⊎ None ⊎ ∅))
exampleObjectImpl (inj₁ x) = inj₁ (ρ 0)
exampleObjectImpl (inj₁ y) = inj₁ ((ρ 0) ∙ (inj₁ x))
exampleObjectImpl (inj₁ z) = inj₁ emptyObj
exampleObjectImpl (inj₁ w) = inj₁ emptyObj
exampleObjectImpl (inj₂ φ) = inj₂ (inj₂ void)

exampleObject : ∀ {Γ} → ExampleLabel ∣ Γ ⊢ ★
exampleObject = makeObject exampleObjectImpl

\end{code}}

\newcommand{\renameDef}{%
\begin{code}

-- renaming
ext : ∀ {Γ Δ} → (∀ {A} → Γ ∋ A → Δ ∋ A)
    -----------------------------------
  → (∀ {A B} → (Γ ,, B) ∋ A → (Δ ,, B) ∋ A)
ext p Z      =  Z
ext p (S a)  =  S (p a)

{-# TERMINATING #-}
rename : ∀ {Γ Δ L}
  → (∀ {A} → Γ ∋ A → Δ ∋ A)
    ------------------------
  → (∀ {A} → L ∣ Γ ⊢ A → L ∣ Δ ⊢ A)
rename p (` a)          =  ` (p a)
rename p (N ∙ l)        =  (rename p N) ∙ l
rename p (N [ a ↦ M ])  =  (rename p N) [ a ↦ (rename p M) ]
rename {Γ} {Δ} {L} p (makeObject N) = makeObject renamedN where
  renamedN : (L ⊎ Phi) → (L ∣ Δ ,, ★ ⊢ ★ ⊎ None ⊎ ∅)
  renamedN l with N l
  ... | inj₁ term = inj₁ (rename (ext p) term)
  ... | inj₂ nonTerm = inj₂ nonTerm

\end{code}}

\newcommand{\simultaneousSubstitution}{%
\begin{code}

 -- simultaneous substitution
exts : ∀ {Γ Δ L} → (∀ {A} → Γ ∋ A → L ∣ Δ ⊢ A)
    ----------------------------------
  → (∀ {A B} → Γ ,, B ∋ A → L ∣ Δ ,, B ⊢ A)
exts σ Z      =  ` Z
exts σ (S a)  =  rename S_ (σ a)

{-# TERMINATING #-}
subst : ∀ {Γ Δ L}
  → (∀ {A} → Γ ∋ A → L ∣ Δ ⊢ A)
    ------------------------
  → (∀ {A} → L ∣ Γ ⊢ A → L ∣ Δ ⊢ A)
subst σ (` k)          =  σ k
subst p (N ∙ l)        =  (subst p N) ∙ l
subst p (N [ a ↦ M ])  =  (subst p N) [ a ↦ (subst p M) ]
subst {Γ} {Δ} {L} p (makeObject N) = makeObject substN where
  substN : (L ⊎ Phi) → (L ∣ Δ ,, ★ ⊢ ★ ⊎ None ⊎ ∅)
  substN l with N l
  ... | inj₁ term = inj₁ (subst (exts p) term)
  ... | inj₂ nonTerm = inj₂ nonTerm

\end{code}}

\newcommand{\simultaneousSubstitutionZeroCase}{%
\begin{code}

subst-zero : ∀ {Γ B L} → (L ∣ Γ ⊢ B) → ∀ {A} → (Γ ,, B ∋ A) → (L ∣ Γ ⊢ A)
subst-zero M Z      =  M
subst-zero M (S a)  =  ` a

_[_] : ∀ {L Γ A B}
        → L ∣ Γ ,, B ⊢ A
        → L ∣ Γ ⊢ B
          ---------
        → L ∣ Γ ⊢ A
_[_] {L} {Γ} {A} {B} N M =  subst {Γ ,, B} {Γ} (subst-zero M) {A} N

singleSubstitution = _[_]

\end{code}}

\newcommand{\applyImpl}{%
\begin{code}

-- reduction

apply : ∀ {L Γ} 
  (N : (L ⊎ Phi) → ((L ∣ (Γ ,, ★) ⊢ ★) ⊎ None ⊎ ∅))
  (c : L)
  (u : L ∣ (Γ ,, ★) ⊢ ★)
  (comp : (a : L ⊎ Phi) → (b : L ⊎ Phi) → Dec (a ≡ b))
  ------------
  → (L ⊎ Phi) → ((L ∣ (Γ ,, ★) ⊢ ★) ⊎ None ⊎ ∅)
apply {L} {Γ} N c u comp attr with comp attr (inj₁ c)
... | yes _ = inj₁ u
... | no _ = N attr

\end{code}}

\newcommand{\reductionImpl}{%
\begin{code}

interleaved mutual

  data _⟿_ : ∀ {L Γ A} → (L ∣ Γ ⊢ A) → (L ∣ Γ ⊢ A) → Set

  singleReductN : ∀ {L Γ}
    {t t' : L ∣ (Γ ,, ★) ⊢ ★}
    {comp : (a : L ⊎ Phi) → (b : L ⊎ Phi) → Dec (a ≡ b)} → (c : L ⊎ Phi)
    → (N : (L ⊎ Phi) → ((L ∣ (Γ ,, ★) ⊢ ★) ⊎ None ⊎ ∅)) 
    → (N c ≡ inj₁ t) → t ⟿ t' 
    → ((L ⊎ Phi) → ((L ∣ (Γ ,, ★) ⊢ ★) ⊎ None ⊎ ∅))
  singleReductN {L} {Γ} {t} {t'} {comp} c N _ _ attr with comp attr c
  ... | yes _ = inj₁ t'
  ... | no _ = N attr

  data _⟿_ where

    congOBJ : ∀ {L Γ c N t_c}
      {t : L ∣ Γ ⊢ ★}
      {t' : L ∣ Γ ,, ★ ⊢ ★}
      {_ : t ≡ makeObject N}
      {eq : N c ≡ inj₁ t_c}
      {comp : (a : L ⊎ Phi) → (b : L ⊎ Phi) → Dec (a ≡ b)}
      → (red : t_c ⟿ t')
        -------------------
      → t ⟿ makeObject (singleReductN {L} {Γ} {t_c} {t'} {comp} c N eq red)
  
    congDOT : ∀ {L Γ} {M M′ : L ∣ Γ ⊢ ★} {l : L}
      → M ⟿ M′
        ----------------
      → M ∙ (inj₁ l) ⟿ M′ ∙ (inj₁ l)

    congAPP₁ : ∀ {L Γ} {N N′ : L ∣ Γ ⊢ ★} {M : L ∣ Γ  ⊢ ★} {l : L}
      → N ⟿ N′
        ----------------
      → N [ l ↦ M ] ⟿ N′ [ l ↦ M ]

    congAPP₂ : ∀ {L Γ} {N : L ∣ Γ ⊢ ★} {M M′ : L ∣ Γ ⊢ ★} {l : L}
      → M ⟿ M′
        ----------------
      → N [ l ↦ M ] ⟿ N [ l ↦ M′ ]
    
    DOT : ∀ {L Γ c N t_c}
      {t : L ∣ Γ ⊢ ★} 
      {_ : t ≡ makeObject N}
      {_ : N c ≡ inj₁ t_c}
      ------------------------
      → t ∙ c ⟿ t_c [ t ]

    DOTφ : ∀ {L Γ c N t_φ} 
      {t : L ∣ Γ ⊢ ★}
      {_ : t ≡ makeObject N}
      {_ : N (inj₁ c) ≡ inj₂ (inj₁ empty)}
      {_ : N (inj₂ φ) ≡ inj₁ t_φ}
      ----------------------
      → t ∙ (inj₁ c) ⟿ t ∙ (inj₂ φ) ∙ (inj₁ c)

    APP : ∀ {L Γ c N}
      {t : L ∣ Γ ⊢ ★}
      {_ : t ≡ makeObject N}
      {_ : N (inj₁ c) ≡ inj₂ (inj₂ void)}
      {u : L ∣ Γ ⊢ ★}
      (comp : (a : L ⊎ Phi) → (b : L ⊎ Phi) → Dec (a ≡ b))
        -----------------------
      → t [ c ↦ u ] ⟿ (makeObject (apply N c (rename S_ u) comp))

\end{code}}

\newcommand{\reductionSequence}{%
\begin{code}
  

infix  2 _⟿*_
infix  1 begin_
infixr 2 _⟿→⟨_⟩_
infix  3 _∎

data _⟿*_ : ∀ {L Γ A} → (L ∣ Γ ⊢ A) → (L ∣ Γ ⊢ A) → Set where

  _∎ : ∀ {L Γ A} (M : L ∣ Γ ⊢ A)
      --------
    → M ⟿* M

  _⟿→⟨_⟩_ : ∀ {L Γ A} (K : L ∣ Γ ⊢ A) {M N : L ∣ Γ ⊢ A}
    → K ⟿ M
    → M ⟿* N
      ---------
    → K ⟿* N

begin_ : ∀ {L} {Γ} {A} {M N : L ∣ Γ ⊢ A}
  → M ⟿* N
    ------
  → M ⟿* N
begin M⟿→N = M⟿→N

\end{code}}

\newcommand{\reductionExampleSupportingLemmas}{%
\begin{code}

-- Supporting lemmas
obtainN : ∀ {L Γ N}
  (t : L ∣ Γ ⊢ ★)
  (_ : t ≡ makeObject N)
  ----------
  → ((L ⊎ Phi) → ((L ∣ Γ ,, ★ ⊢ ★) ⊎ None ⊎ ∅))
obtainN (makeObject N) _ = N

makeObjectEqualityLemma : ∀ {L Γ}
  {N M : (L ⊎ Phi)
  → ((L ∣ (Γ ,, ★) ⊢ ★) ⊎ None ⊎ ∅)}
  → (N ≡ M)
  --------------
  → makeObject N ≡ makeObject M
makeObjectEqualityLemma e rewrite e = _≡_.refl

⟿FromEqualityLemma : ∀ {L Γ}
  {N M M′ : L ∣ Γ ⊢ ★}
  → (N ⟿ M) → (M ≡ M′)
  -------------
  → (N ⟿ M′)
⟿FromEqualityLemma a e rewrite e = a

\end{code}}

\newcommand{\reductionExampleStepsDefinitions}{%
\begin{code}

-- Steps definitions

-- [[ y ↦ ∅ ]]
innerObjectLeftPartImpl_reductionExample : 
  ((ExampleLabel ⊎ Phi) → ((ExampleLabel ∣ (none ,, ★ ,, ★) ⊢ ★) ⊎ None ⊎ ∅))
innerObjectLeftPartImpl_reductionExample = \ {
      (inj₁ y) -> inj₂ (inj₂ void) ; 
      (inj₁ x) -> inj₂ (inj₁ empty) ;
      (inj₁ z) -> inj₂ (inj₁ empty) ;
      (inj₁ w) -> inj₂ (inj₁ empty) ;
      (inj₂ φ) -> inj₂ (inj₁ empty) }

innerObjectLeftPart_reductionExample : ExampleLabel ∣ (none ,, ★) ⊢ ★
innerObjectLeftPart_reductionExample = 
  makeObject innerObjectLeftPartImpl_reductionExample

-- [[ x ↦ [[ y ↦ ∅ ]] ]]
leftPartImpl_reductionExample : 
  ((ExampleLabel ⊎ Phi) → ((ExampleLabel ∣ (none ,, ★) ⊢ ★) ⊎ None ⊎ ∅))
leftPartImpl_reductionExample = \{
  (inj₁ x) -> inj₁ (innerObjectLeftPart_reductionExample) ; 
  (inj₁ y) -> inj₂ (inj₁ empty) ;
  (inj₁ z) -> inj₂ (inj₁ empty) ;
  (inj₁ w) -> inj₂ (inj₁ empty) ;
  (inj₂ φ) -> inj₂ (inj₁ empty)}

-- [[ x ↦ [[ y ↦ ∅ ]] ]].x
leftPart_reductionExample : ExampleLabel ∣ none ⊢ ★
leftPart_reductionExample =
  (makeObject leftPartImpl_reductionExample) ∙ (inj₁ x)

-- emptyObj = [[]]
-- [[ z ↦ emptyObj ]]
innerObjectImpl_reductionExample :
  ((ExampleLabel ⊎ Phi) → ((ExampleLabel ∣ (none ,, ★) ⊢ ★) ⊎ None ⊎ ∅))
innerObjectImpl_reductionExample = \ {  
  (inj₁ z) -> (inj₁ emptyObj) ;
  (inj₁ x) -> inj₂ (inj₁ empty) ;
  (inj₁ y) -> inj₂ (inj₁ empty) ;
  (inj₁ w) -> inj₂ (inj₁ empty) ;
  (inj₂ φ) -> inj₂ (inj₁ empty) }

innerObject_reductionExample : ExampleLabel ∣ none ⊢ ★
innerObject_reductionExample = makeObject innerObjectImpl_reductionExample

-- [[ x ↦ [[ y ↦ ∅ ]] ]].x (y ↦ [[ z ↦ emptyObj ]].z)
firstStep_reductionExample : ExampleLabel ∣ none ⊢ ★
firstStep_reductionExample =
  leftPart_reductionExample [ y ↦ (innerObject_reductionExample ∙ (inj₁ z)) ]

-- [[ x ↦ [[ y ↦ ∅ ]] ]].x (y ↦ emptyObj)
secondStep_reductionExample : ExampleLabel ∣ none ⊢ ★
secondStep_reductionExample = leftPart_reductionExample [ y ↦ emptyObj ]

-- [[ y ↦ ∅ ]]
thirdStepImpl_reductionExample :
  ((ExampleLabel ⊎ Phi) → ((ExampleLabel ∣ (none ,, ★) ⊢ ★) ⊎ None ⊎ ∅))
thirdStepImpl_reductionExample = \ {  
  (inj₁ y) -> inj₂ (inj₂ void) ; 
  (inj₁ x) -> inj₂ (inj₁ empty) ;
  (inj₁ z) -> inj₂ (inj₁ empty) ;
  (inj₁ w) -> inj₂ (inj₁ empty) ;
  (inj₂ φ) -> inj₂ (inj₁ empty) }

-- [[ y ↦ ∅ ]] (y ↦ emptyObj)
thirdStep_reductionExample : ExampleLabel ∣ none ⊢ ★
thirdStep_reductionExample =
  (makeObject thirdStepImpl_reductionExample) [ y ↦ emptyObj ]


-- [[ y ↦ emptyObj ]]
fourthStepImpl_reductionExample :
  ((ExampleLabel ⊎ Phi) → ((ExampleLabel ∣ (none ,, ★) ⊢ ★) ⊎ None ⊎ ∅))
fourthStepImpl_reductionExample = \ {  
  (inj₁ y) -> inj₁ emptyObj ; 
  (inj₁ x) -> inj₂ (inj₁ empty) ;
  (inj₁ z) -> inj₂ (inj₁ empty) ;
  (inj₁ w) -> inj₂ (inj₁ empty) ;
  (inj₂ φ) -> inj₂ (inj₁ empty) }

-- [[ y ↦ emptyObj ]]
fourthStep_reductionExample : ExampleLabel ∣ none ⊢ ★
fourthStep_reductionExample = makeObject fourthStepImpl_reductionExample


\end{code}}

\newcommand{\reductionExampleStepOneTwo}{%
\begin{code}

-- Reduction steps proofs

-- firstStep ⟿ secondStep
-- [[ x ↦ [[ y ↦ ∅ ]] ]].x (y ↦ [[ z ↦ emptyObj ]].z)
--  ⟿ [[ x ↦ [[ y ↦ ∅ ]] ]].x (y ↦ emptyObj)
-- Here we apply DOT and congAPP₂ rules
firstToSecondStepLemma_reductionExample :
  firstStep_reductionExample ⟿ secondStep_reductionExample
firstToSecondStepLemma_reductionExample =
  congAPP₂ (DOT 
    {ExampleLabel}
    {none}
    {inj₁ z}
    {innerObjectImpl_reductionExample}
    {emptyObj}
    {innerObject_reductionExample}
    {_≡_.refl} {_≡_.refl})


\end{code}}

\newcommand{\reductionExampleStepTwoThree}{%
\begin{code}

-- secondStep ⟿ thirdStep
-- [[ x ↦ [[ y ↦ ∅ ]] ]].x (y ↦ emptyObj)
--  ⟿ [[ y ↦ ∅ ]] (y ↦ emptyObj)
-- Here we apply DOT and congAPP₁ rules
-- LP means "Left Part"
dotToSecondStepLPImpl≡ThirdStepLPImplLemma_reductionExample :
  obtainN ( innerObjectLeftPart_reductionExample 
    [ makeObject leftPartImpl_reductionExample ])
  _≡_.refl ≡ thirdStepImpl_reductionExample
dotToSecondStepLPImpl≡ThirdStepLPImplLemma_reductionExample =
  φ-extensionality _ thirdStepImpl_reductionExample eqAttr where
    eqAttr : (attr : ExampleLabel ⊎ Phi)
      → obtainN (innerObjectLeftPart_reductionExample
        [ makeObject leftPartImpl_reductionExample ])
      _≡_.refl attr ≡ thirdStepImpl_reductionExample attr
    eqAttr attr with attr
    ... | inj₁ x = _≡_.refl
    ... | inj₁ y = _≡_.refl
    ... | inj₁ z = _≡_.refl
    ... | inj₁ w = _≡_.refl
    ... | inj₂ φ = _≡_.refl

dotToSecondStepLP≡ThirdStepLemmaLP_reductionExample :
  (innerObjectLeftPart_reductionExample
    [ makeObject leftPartImpl_reductionExample ])
  ≡ (makeObject thirdStepImpl_reductionExample)
dotToSecondStepLP≡ThirdStepLemmaLP_reductionExample =
  makeObjectEqualityLemma
    {ExampleLabel}
    {none}
    {obtainN
      (innerObjectLeftPart_reductionExample
        [ makeObject leftPartImpl_reductionExample ])
      _≡_.refl}
    {thirdStepImpl_reductionExample}
    dotToSecondStepLPImpl≡ThirdStepLPImplLemma_reductionExample

dotToSecondStep≡ThirdStepLemma_reductionExample :
  (innerObjectLeftPart_reductionExample
    [ makeObject leftPartImpl_reductionExample ])
  [ y ↦ emptyObj ]
  ≡ thirdStep_reductionExample
dotToSecondStep≡ThirdStepLemma_reductionExample
  rewrite dotToSecondStepLP≡ThirdStepLemmaLP_reductionExample
  = _≡_.refl

secondStepToThirdStepLemma_reductionExample :
  secondStep_reductionExample ⟿ thirdStep_reductionExample
secondStepToThirdStepLemma_reductionExample =
  ⟿FromEqualityLemma (congAPP₁
    (DOT
      {ExampleLabel}
      {none}
      {inj₁ x}
      {leftPartImpl_reductionExample}
      {innerObjectLeftPart_reductionExample}
      {_} {_≡_.refl} {_≡_.refl}))
    dotToSecondStep≡ThirdStepLemma_reductionExample


\end{code}}

\newcommand{\reductionExampleStepThreeFour}{%
\begin{code}


-- thirdStep ⟿ fourthStep
-- [[ y ↦ ∅ ]] (y ↦ emptyObj) ⟿ [[ y ↦ emptyObj ]]
-- Here we apply APP rule
applyToThirdStep≡ForthStep1Lemma_reductionExample :
  (apply thirdStepImpl_reductionExample y
    (rename S_ emptyObj) exampleLabelComparator)
  ≡ fourthStepImpl_reductionExample
applyToThirdStep≡ForthStep1Lemma_reductionExample =
  (φ-extensionality _ fourthStepImpl_reductionExample \ {
    (inj₁ x) → _≡_.refl ;
    (inj₁ y) → _≡_.refl ;
    (inj₁ z) → _≡_.refl ;
    (inj₁ w) → _≡_.refl ;
    (inj₂ φ) → _≡_.refl })

applyToThirdStep≡ForthStep2Lemma_reductionExample :
  makeObject
    (apply thirdStepImpl_reductionExample y
      (rename S_ emptyObj) exampleLabelComparator)
  ≡ fourthStep_reductionExample
applyToThirdStep≡ForthStep2Lemma_reductionExample =
  makeObjectEqualityLemma
    applyToThirdStep≡ForthStep1Lemma_reductionExample

thirdToForthStepLemma_reductionExample :
  thirdStep_reductionExample ⟿ fourthStep_reductionExample
thirdToForthStepLemma_reductionExample = ⟿FromEqualityLemma 
  (APP 
    {ExampleLabel} 
    {_} {y} 
    {thirdStepImpl_reductionExample} 
    {_} {_≡_.refl} {_≡_.refl} 
    {emptyObj} 
    exampleLabelComparator)
  applyToThirdStep≡ForthStep2Lemma_reductionExample


\end{code}}

\newcommand{\reductionExampleCompleteSequence}{%
\begin{code}

-- Actual reduction sequence example

-- [[ x ↦ [[ y ↦ ∅ ]] ]].x (y ↦ [[ z ↦ emptyObj ]].z)
-- ⟿
-- [[ x ↦ [[ y ↦ ∅ ]] ]].x (y ↦ emptyObj)
-- ⟿
-- [[ y ↦ ∅ ]] (y ↦ emptyObj)
-- ⟿
-- [[ y ↦ emptyObj ]]
reductionExample : firstStep_reductionExample ⟿* fourthStep_reductionExample
reductionExample =
  begin
    firstStep_reductionExample
  ⟿→⟨ firstToSecondStepLemma_reductionExample ⟩
    secondStep_reductionExample
  ⟿→⟨ secondStepToThirdStepLemma_reductionExample ⟩
    thirdStep_reductionExample
  ⟿→⟨ thirdToForthStepLemma_reductionExample ⟩
    fourthStep_reductionExample
  ∎

\end{code}}

\newcommand{\remainingImpl}{%
\begin{code}

-- parallel reduction

interleaved mutual

  data _⇛_ : ∀ {L Γ A} → (L ∣ Γ ⊢ A) → (L ∣ Γ ⊢ A) → Set

  reductionMapsProof : ∀ {L Γ} → (N_attr : ((L ∣ (Γ ,, ★) ⊢ ★) ⊎ None ⊎ ∅)) 
    → (t' : (L ∣ Γ ,, ★ ⊢ ★)) → Set
  reductionMapsProof {L} {Γ} N_attr t' with (N_attr)
  ... | (inj₁ t) = t ⇛ t'
  ... | (inj₂ v) = ⊤

  parallelReductN : ∀ {L Γ}
    → (N : (L ⊎ Phi) → ((L ∣ (Γ ,, ★) ⊢ ★) ⊎ None ⊎ ∅))
    → (reductionMaps : (attr : L ⊎ Phi) → (L ∣ (Γ ,, ★) ⊢ ★))
    → ((L ⊎ Phi) → ((L ∣ (Γ ,, ★) ⊢ ★) ⊎ None ⊎ ∅))
  parallelReductN {L} {Γ} N reductionMaps c with N c
  ... | (inj₁ t) = inj₁ (reductionMaps c)
  ... | (inj₂ v) = inj₂ v

  decomposeParallelReductLemma : ∀ {L Γ c t}
    {N : (L ⊎ Phi) → ((L ∣ (Γ ,, ★) ⊢ ★) ⊎ None ⊎ ∅)}
    {reductionMaps : (attr : L ⊎ Phi) → (L ∣ (Γ ,, ★) ⊢ ★)}
    → (e : N c ≡ inj₁ t)
    → (parallelReductN N reductionMaps) c ≡  inj₁ (reductionMaps c)
  decomposeParallelReductLemma
    {L} {Γ} {c} {t} {N} {reductionMaps} e rewrite e = _≡_.refl

  data _⇛_ where

    cong⇛P : ∀ {L Γ A} {x : Γ ∋ A}
      ---------
      → (`_ {L} {Γ} {A} x) ⇛ (` x)
    
    cong⇛DOT : ∀ {L Γ c} {t t' : L ∣ Γ ⊢ ★}
      → t ⇛ t'
      ---------------------------------
      → t ∙ c ⇛ t' ∙ c

    cong⇛APP : ∀ {L Γ c} {t t' u u' : L ∣ Γ ⊢ ★}
      → t ⇛ t'
      → u ⇛ u'
      ---------------------------
      → t [ c ↦ u ] ⇛ t' [ c ↦ u' ]
    
    DOT⇛ : ∀ {L Γ c N t_c}
      {t t' : L ∣ Γ ⊢ ★} 
      {_ : t' ≡ makeObject N}
      {_ : N c ≡ inj₁ t_c}
      → t ⇛ t'
      ------------------------
      → t ∙ c ⇛ t_c [ t' ]
    
    DOT⇛φ : ∀ {L Γ c N t_φ}
      {t t' : L ∣ Γ ⊢ ★}
      {_ : t' ≡ makeObject N}
      {_ : N (inj₁ c) ≡ inj₂ (inj₁ empty)}
      {_ : N (inj₂ φ) ≡ inj₁ t_φ}
      → t ⇛ t'
      ----------------------
      → t ∙ (inj₁ c) ⇛ t' ∙ (inj₂ φ) ∙ (inj₁ c)
    
    APP⇛ : ∀ {L Γ c N}
      {t t' : L ∣ Γ ⊢ ★}
      {_ : t' ≡ makeObject N}
      {_ : N (inj₁ c) ≡ inj₂ (inj₂ void)}
      {u : L ∣ Γ ⊢ ★}
      {comp : (a : L ⊎ Phi) → (b : L ⊎ Phi) → Dec (a ≡ b)}
      → t ⇛ t'
        -----------------------
      → t [ c ↦ u ] ⇛ (makeObject (apply N c (rename S_ u) comp))
    
    cong⇛OBJ : ∀ {L Γ N}
      {t : L ∣ Γ ⊢ ★}
      {_ : t ≡ makeObject N}
      → (reductionMaps : (L ⊎ Phi) → (L ∣ Γ ,, ★ ⊢ ★))
      → (reductionProofs :
        (attr : L ⊎ Phi) → reductionMapsProof (N attr) (reductionMaps attr))
      -----------------------------
      → t ⇛ makeObject (parallelReductN N reductionMaps)

⇛FromEqualityLemma :
  ∀ {L Γ} {N M M′ : L ∣ Γ ⊢ ★} → (N ⇛ M) → (M ≡ M′) → (N ⇛ M′)
⇛FromEqualityLemma a e rewrite e = a

interleaved mutual

  ⇛-reflexive : ∀ {L Γ A} → (t : L ∣ Γ ⊢ A) → t ⇛ t

  reductToSelfMap : ∀ {L Γ}
    → (N : (L ⊎ Phi)→ ((L ∣ (Γ ,, ★) ⊢ ★) ⊎ None ⊎ ∅))
    → ((attr : L ⊎ Phi) → (L ∣ Γ ,, ★ ⊢ ★))
  reductToSelfMap N c with N c
  ... | (inj₁ t) = t
  ... | (inj₂ _) = emptyObj

  reductToSelfProof : ∀ {L Γ}
    {N : (L ⊎ Phi) → ((L ∣ (Γ ,, ★) ⊢ ★) ⊎ None ⊎ ∅)}
    → (attr : L ⊎ Phi)
    → reductionMapsProof (N attr) ((reductToSelfMap N) attr)
  reductToSelfProof {L} {Γ} {N} attr with N attr
  ... | (inj₁ t) = ⇛-reflexive t
  ... | (inj₂ v) = tt 

  reductToSelfReflLemma : ∀ {L Γ}
    {N : (L ⊎ Phi) → ((L ∣ (Γ ,, ★) ⊢ ★) ⊎ None ⊎ ∅)}
    → (attr : L ⊎ Phi)
    → (t : L ∣ (Γ ,, ★) ⊢ ★)
    → (eq : N attr ≡ inj₁ t)
    → (reductToSelfMap N) attr ≡ t
  reductToSelfReflLemma {L} {Γ} {N} attr t eq rewrite eq = _≡_.refl

  parallelReductOnSelfReflexivity : ∀ {L Γ N}
    → (parallelReductN {L} {Γ} N (reductToSelfMap N)) ≡ N
  parallelReductOnSelfReflexivity {L} {Γ} {N} =
    φ-extensionality (parallelReductN N (reductToSelfMap N)) N eqC where
      eqC : (c : L ⊎ Phi)
        → ((parallelReductN N (reductToSelfMap N)) c ≡ N c)
      eqC c with N c in eq
      ... | (inj₁ t) rewrite reductToSelfReflLemma {L} {Γ} {N} c t eq =
        _≡_.refl
      ... | (inj₂ v) = _≡_.refl

  ⇛-reflexive (` x₁) = cong⇛P
  ⇛-reflexive (t ∙ c) = cong⇛DOT (⇛-reflexive t)
  ⇛-reflexive (t [ c ↦ u ]) = cong⇛APP (⇛-reflexive t) (⇛-reflexive u)
  ⇛-reflexive {L} {Γ} {★} (makeObject N) = 
    ⇛FromEqualityLemma {L} {Γ}
      {makeObject N}
      {makeObject (parallelReductN {L} {Γ} N (reductToSelfMap N))}
      {makeObject N}
      (cong⇛OBJ {L} {Γ} {N} {_} {_≡_.refl}
        (reductToSelfMap N) (reductToSelfProof))
      (makeObjectEqualityLemma
        {L} {Γ}
        {parallelReductN {L} {Γ} N (reductToSelfMap N)}
        {N} (parallelReductOnSelfReflexivity))

interleaved mutual

  singleReductionImpliesParallel : ∀ {L Γ A}
    {t t' : L ∣ Γ ⊢ A} → t ⟿ t' → t ⇛ t'

  parallelFromSingleMap : ∀ {L Γ}
    {comp : (a : L ⊎ Phi) → (b : L ⊎ Phi) → Dec (a ≡ b)}
    → (N : (L ⊎ Phi) → ((L ∣ (Γ ,, ★) ⊢ ★) ⊎ None ⊎ ∅)) 
    → (c : L ⊎ Phi) 
    → (t t' : L ∣ Γ ,, ★ ⊢ ★) 
    → (e : (N c) ≡ (inj₁ t)) 
    → (red : t ⟿ t') 
    → ((attr : L ⊎ Phi) → (L ∣ Γ ,, ★ ⊢ ★))
  parallelFromSingleMap {L} {Γ} {comp} N c t t' e red attr with comp attr c
  ... | yes _ = t'
  ... | no _ with N attr
  ...   | (inj₁ term) = term
  ...   | (inj₂ v) = emptyObj

  parallelFromSingleMapC≡t' : ∀ {L Γ}
    {comp : (a : L ⊎ Phi) → (b : L ⊎ Phi) → Dec (a ≡ b)}
    → (N : (L ⊎ Phi) → ((L ∣ (Γ ,, ★) ⊢ ★) ⊎ None ⊎ ∅)) 
    → (c : L ⊎ Phi) 
    → (t t' : L ∣ Γ ,, ★ ⊢ ★) 
    → (e : (N c) ≡ (inj₁ t)) 
    → (red : t ⟿ t') 
    → (parallelFromSingleMap {L} {Γ} {comp} N c t t' e red) c ≡ t'
  parallelFromSingleMapC≡t' {L} {Γ} {comp} N c t t' e red with comp c c
  ... | yes _ = _≡_.refl
  ... | no p = ⊥-elim (p _≡_.refl)

  paralellFromSingleMapNotC≡term : ∀ {L Γ}
    {comp : (a : L ⊎ Phi) → (b : L ⊎ Phi) → Dec (a ≡ b)}
    → (N : (L ⊎ Phi) → ((L ∣ (Γ ,, ★) ⊢ ★) ⊎ None ⊎ ∅)) 
    → (c : L ⊎ Phi) 
    → (t t' : L ∣ Γ ,, ★ ⊢ ★) 
    → (e : (N c) ≡ (inj₁ t)) 
    → (red : t ⟿ t')
    → (attr : L ⊎ Phi)
    → (notP : attr ≡ c → ⊥)
    → (term : L ∣ Γ ,, ★ ⊢ ★)
    → (eq : N attr ≡ inj₁ term)
    → (parallelFromSingleMap {L} {Γ} {comp} N c t t' e red) attr ≡ term
  paralellFromSingleMapNotC≡term {L} {Γ} {comp}
    N c t t' e red attr notP term eq with comp attr c
  ... | yes p = ⊥-elim (notP p)
  ... | no _ rewrite eq = _≡_.refl

  parallelFromSingleProof : ∀ {L Γ}
    {comp : (a : L ⊎ Phi) → (b : L ⊎ Phi) → Dec (a ≡ b)}
    → (N : (L ⊎ Phi) → ((L ∣ (Γ ,, ★) ⊢ ★) ⊎ None ⊎ ∅))
    → (c : L ⊎ Phi) 
    → (t t' : L ∣ Γ ,, ★ ⊢ ★) 
    → (e : (N c) ≡ (inj₁ t)) 
    → (red : t ⟿ t') 
    → (attr : L ⊎ Phi)
    → reductionMapsProof
      (N attr) (parallelFromSingleMap {L} {Γ} {comp} N c t t' e red attr)
  parallelFromSingleProof {L} {Γ} {comp} N c t t' e red attr with comp attr c
  ... | yes e_attr_c rewrite e_attr_c | e =
    singleReductionImpliesParallel {L} {Γ ,, ★} {★} {t} {t'} red
  parallelFromSingleProof {L} {Γ} {comp} N c t t' e red attr
      | no _ with N attr
  ...   | (inj₁ term) = ⇛-reflexive term
  ...   | (inj₂ v) = tt

  singleParallelTransitionEqualityAttr : ∀ {L Γ}
    {comp : (a : L ⊎ Phi) → (b : L ⊎ Phi) → Dec (a ≡ b)}
    → (N : (L ⊎ Phi) → ((L ∣ (Γ ,, ★) ⊢ ★) ⊎ None ⊎ ∅))
    → (c : L ⊎ Phi) 
    → (t t' : L ∣ Γ ,, ★ ⊢ ★) 
    → (e : (N c) ≡ (inj₁ t)) 
    → (red : t ⟿ t') 
    → (attr : L ⊎ Phi)
    → (parallelReductN
      N (parallelFromSingleMap {L} {Γ} {comp} N c t t' e red)) attr
      ≡ (singleReductN {L} {Γ} {t} {t'} {comp} c N e red) attr
  singleParallelTransitionEqualityAttr
    {L} {Γ} {comp} N c t t' e red attr with comp attr c
  ... | yes e_attr_c with attr | e_attr_c
  ...   | .c | _≡_.refl rewrite
    (decomposeParallelReductLemma
      {L} {Γ} {c} {t} {N}
      {(parallelFromSingleMap {L} {Γ} {comp} N c t t' e red)} e)
    | (parallelFromSingleMapC≡t' {L} {Γ} {comp} N c t t' e red) = _≡_.refl
  singleParallelTransitionEqualityAttr {L} {Γ} {comp} N c t t' e red attr
      | no notP with N attr in eq
  ...   | (inj₁ term) rewrite
    (decomposeParallelReductLemma
      {L} {Γ} {attr} {term} {N}
      {(parallelFromSingleMap {L} {Γ} {comp} N c t t' e red)} eq)
    | (paralellFromSingleMapNotC≡term
      {L} {Γ} {comp} N c t t' e red attr notP term eq) = _≡_.refl
  ...   | (inj₂ v) = _≡_.refl

  singleParallelTransitionEquality : ∀ {L Γ}
    {comp : (a : L ⊎ Phi) → (b : L ⊎ Phi) → Dec (a ≡ b)}
    → (N : (L ⊎ Phi) → ((L ∣ (Γ ,, ★) ⊢ ★) ⊎ None ⊎ ∅))
    → (c : L ⊎ Phi) 
    → (t t' : L ∣ Γ ,, ★ ⊢ ★) 
    → (e : (N c) ≡ (inj₁ t)) 
    → (red : t ⟿ t') 
    → makeObject (parallelReductN N
      (parallelFromSingleMap {L} {Γ} {comp} N c t t' e red))
      ≡ makeObject (singleReductN {L} {Γ} {t} {t'} {comp} c N e red)
  singleParallelTransitionEquality {L} {Γ} {comp} N c t t' e red =
    makeObjectEqualityLemma (φ-extensionality
      (parallelReductN N
        (parallelFromSingleMap {L} {Γ} {comp} N c t t' e red))
      (singleReductN {L} {Γ} {t} {t'} {comp} c N e red)
      (singleParallelTransitionEqualityAttr {L} {Γ} {comp} N c t t' e red))

  singleReductionImpliesParallel {L} {Γ} {★} {t} {t'} red with red
  ... | DOT {L} {Γ} {c} {N} {t_c} {t} {e1} {e2} =
    DOT⇛ {L} {Γ} {c} {N} {t_c} {t} {t} {e1} {e2} (⇛-reflexive t)
  ... | congDOT {L} {Γ} {_} {_} {l} r =
    cong⇛DOT (singleReductionImpliesParallel r)
  ... | congAPP₁ {L} {Γ} {_} {_} {M} r =
    cong⇛APP (singleReductionImpliesParallel r) (⇛-reflexive M)
  ... | congAPP₂ {L} {Γ} {N} {_} {_} r =
    cong⇛APP (⇛-reflexive N) (singleReductionImpliesParallel r)
  ... | DOTφ {L} {Γ} {c} {N} {t_φ} {t} {e1} {e2} {e3} =
    DOT⇛φ {L} {Γ} {c} {N} {t_φ} {t} {t} {e1} {e2} {e3} (⇛-reflexive t)
  ... | APP {L} {Γ} {c} {N} {t'} {e1} {e2} {u} comp =
    APP⇛ {L} {Γ} {c} {N} {t'} {t'} {e1} {e2} {u} {comp} (⇛-reflexive t')
  ... | congOBJ {L} {Γ} {c} {N} {t_c} {t} {t''} {e1} {e2} {comp} r =
    ⇛FromEqualityLemma {L} {Γ} {_} {_} {_}
      (cong⇛OBJ {L} {Γ} {N} {t} {e1}
        (parallelFromSingleMap {L} {Γ} {comp} N c t_c t'' e2 r)
        (parallelFromSingleProof {L} {Γ} {comp} N c t_c t'' e2 r))
      (singleParallelTransitionEquality {L} {Γ} {comp} N c t_c t'' e2 r)

\end{code}}         